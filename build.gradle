plugins {
    id 'org.spongepowered.plugin' version '0.9.0'
}

group = 'us.racem'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'enginehub-maven'
        url = 'https://maven.enginehub.org/repo/'
    }
}

dependencies {
    compileOnly 'org.spongepowered:spongeapi:7.4.0'
    annotationProcessor 'org.spongepowered:spongeapi:7.4.0'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    compileOnly 'com.sk89q.worldedit:worldedit-sponge:7.3.0-SNAPSHOT'
    compileOnly 'com.sk89q.worldedit:worldedit-core:6.1.4-SNAPSHOT'

    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'wang.harlon.quickjs:wrapper-java:0.6.0'
    implementation 'org.apache.commons:commons-collections4:4.4'
}

sponge.plugin.id = 'guilds'

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

tasks.withType(Test) {enabled = false}

task make_server(type: Exec) {
    workingDir "${buildDir}"
    commandLine 'bash', "${projectDir}/scripts/do_server.sh", "${jar.archivePath}"
}

task run_server(type: Exec) {
    workingDir "${buildDir}"
    commandLine 'bash', "${projectDir}/scripts/do_run.sh", "${buildDir}"
}